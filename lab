	.model tiny
	.code
	.186
	org 100h


eic		macro	cond, str     				;Exit if condition is met
		LOCAL	exitMacro, writeError
	                    
		cond	writeError	                    
		jmp		exitMacro
				                    	
writeError:	  
	   	mov		ah, 09h
	   	lea		dx, str
	   	int		21h
	   			
	   	mov		ah, 4Ch
		int		21h       
				
exitMacro:
endm 	          	
                                
	
start:          
	mov		ch, 0 
	mov		cl, es:[0080h]
	mov		si, 81h
	lea		di, numberOfLaunches    
	mov		bx, 127 
	call	readParam	
	lea		di, buff
	mov		bx, 5
	call	readParam
	
	cmp		numberOfLaunches[0], 0 
	eic		je, neParams
	cmp		buff[0], 0 
	eic		jne, tmParams                                                        
	                      
	lea		di, numberOfLaunches
	call	atoi   
	mov		cx, ax
	lea		dx, programPath 
	 
launchLoop:	    
	call	startApp	            	               
	loop	launchLoop	                      
	                             
	mov		ah, 4Ch  
	int		21h


atoi proc			
		push	bx
		push	dx
	
		mov		ax, 0  
	 
atoiLoop:
		cmp		byte ptr [di], 0   
		je		exitAtoi
	 
		cmp		byte ptr [di], '0'
		eic		jl, wrongNumber		
		cmp		byte ptr [di], '9' 
		eic		jg, wrongNumber    
			      
		mov		bx, 10 
		mul		bx
		mov		bl, [di]
		sub		bl, '0' 
		add		ax, bx  
		
		cmp		ax, 255  
		eic		jg, wrongNumber 					      
			            
		inc		di	;++          
		jmp		atoiLoop
		
exitAtoi:							    
		cmp		ax, 1 
		eic		jl, wrongNumber
		cmp		ax, 255
		eic		jg, wrongNumber
			        	
		pop		dx
		pop		bx			        	
		ret	        
endp	atoi


startApp proc								;Starts app with path in DX	      
		push	ax							
		push	bx							
		push	dx	            

		mov		esVal, es					
		mov		dsVal, ds
		mov		ssVal, ss
		mov		spVal, sp
		
		mov		sp, memorySize				
		                                    
		mov		ah, 4Ah						
	
		mov		bx, memorySize shr 4 + 1	
		int		21h      					;Free memory after program end
		
		eic		jc, memModError    			
		              		             		             
		mov		ax, cs							
		mov		word ptr EPB + 4, ax		
		mov		word ptr EPB + 8, ax		
		mov		word ptr EPB + 0Ch, ax		
		                                
		mov		ax, 4B00h   
		lea		bx, EPB     
		int		21h							;Launch program	    
		
		mov		es, cs:esVal 
		mov		ds, cs:dsVal
		mov		ss, cs:ssVal
		mov		sp, cs:spVal

		eic		jc, startError		                  				                 		                 		  		        				         	                	               	                           				                           	                           	
	           
		pop		dx	           
		pop		bx
		pop		ax	        	
	                
		ret	            
endp	startApp




	
readParam proc			
		push	ax	  
		push	bx     	          	
	           
		cmp		cx, 0	           
	 	jle		exitReadParam
	        	      	                  	                  
skipSpaces:      
		push	di
		mov		di, si
		
		mov		al, ' '
		repz	scasb	   
		dec		di   
		inc		cx
		mov		si, di  
		
		pop		di
		        
findParamEnd:
		movsb      
		dec		bx
		dec		cx
		cmp		bx, 0
		je		paramEnded
		cmp		byte ptr es:[di - 1], 0Dh
		je		paramEnded
		cmp		byte ptr es:[di - 1], ' '
		jne		findParamEnd
		
paramEnded:	   		
		dec		di    
		mov		byte ptr es:[di], 0
		inc		di

exitReadParam:	  
		pop		bx   
		pop		ax	     		        	
		ret	            	
endp	readParam 

		programPath	db	"C:\app.com", 0
                                 
		memModError	db "! Can't modify memory block$"
		startError	db "! Can't start app$"                                  
		wrongNumber	db "! Please, enter number between 1 and 255$"                               
		neParams	db "! Not enough params$"               
		tmParams	db "! Too many params$"
		                     
		numberOfLaunches	db	128 dup(0) 
		buff				db	5 dup(0)
		
		esVal			dw 0
		dsVal			dw 0
		ssVal			dw 0
		spVal			dw 0	                      	
		                      
		EPB			dw 0000 ;
					dw offset commandLine, 0 
					dw 005Ch, 0, 006Ch, 0  
		commandLine	db 0, 0Dh   
                                                                      
		memorySize	EQU $ - start + 100h + 200h                              
                               
end		start
